resource "aws_acm_certificate" "ssl-cert" {
    domain_name       = var.domain_name
    validation_method = "DNS"
  
    tags = {
      "product"        = var.name
      "environment"    = var.environment
      "role"           = "networking"
      "terraform"      = "true"    
    }

    subject_alternative_names = [
      "*.${var.domain_name}"
    ]
  
    lifecycle {
      create_before_destroy = true
    }
  }
  
resource "aws_route53_record" "lifecycle-cert-validation" {
  for_each = {
    for dvo in aws_acm_certificate.ssl-cert.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true
  name            = each.value.name
  records         = [each.value.record]
  ttl             = 60
  type            = each.value.type
  zone_id         = var.route53_zoneid
}

# Output the ARN of the ACM certificate
output "ssl_cert_arn" {
  value = aws_acm_certificate.ssl-cert.arn
}